plugins {
	id 'org.springframework.boot' version '2.4.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.simpletodolist'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
	set('snippetsDir', file("build/generated-snippets")) //문서 조각인 스니펫이 생성되는 경로
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
	attributes \
		'snippets': snippetsDir
}

bootJar {
	dependsOn asciidoctor// generate documentation before jar
	from ("${asciidoctor.outputDir}") {
		into 'static/docs'
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-mustache'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
    compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.5.RELEASE'
	testImplementation 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.5.RELEASE'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

test {
	outputs.dir snippetsDir // add 'snippetsDir' as output.
	useJUnitPlatform()
}

