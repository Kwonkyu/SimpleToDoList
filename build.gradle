plugins {
	id 'org.springframework.boot' version '2.4.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.asciidoctor.convert' version '1.5.9.2'
}

group = 'com.simpletodolist'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'


ext { // ext block for defining extra properties.
	snippetsDir = file('build/generated-snippets') // snippets generated by rest docs is located at this directory.
}

asciidoctor {
	dependsOn test
	inputs.dir snippetsDir // configure snippetsDir as input of asciidoctor.
	attributes \
		'snippets': snippetsDir
}

asciidoctor.doFirst {
	delete file("src/main/resources/static/docs") // delete old documents.
}

task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/asciidoc/html5")
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyDocument
}

bootJar {
	dependsOn copyDocument // generate documentation before jar
	from('${asciidoctor.outputDir}/html5') { // generated 'html' files from 'adoc' is located at build directory.
		into 'static/docs' // move these documentations into static of jar
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-mustache'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
    compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.5.RELEASE'
	testImplementation 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.5.RELEASE'
}

test {
	outputs.dir snippetsDir // add 'snippetsDir' as output.
	useJUnitPlatform()
}
